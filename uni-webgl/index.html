<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>WebGl Js</title>

    <!-- External javascript file -->
    <script type="text/javascript" src="./libs/glMatrix.js"></script>
    <script type="text/javascript" src="./libs/webgl-debug.js"></script>
    <script type="text/javascript" src="./libs/webgl-utils.js"></script>

    <!-- Vertex shader javascript -->
    <script id="shader-vs" type="x-shader/x-vertex">
      attribute vec3 aVertexPosition;
      attribute vec3 aVertexNormal;
      attribute vec2 aTextureCoordinates;

      uniform mat4 uMVMatrix;
      uniform mat4 uPMatrix;

      uniform mat3 uNMatrix;

      varying vec2 vTextureCoordinates;
      varying vec3 vNormalEye;
      varying vec3 vPositionEye3;

      void main() {
        // Get the vertex position in camera/eye coordinates and send
        // to the fragment shader
        vec4 vertexPositionEye4 = uMVMatrix * vec4(aVertexPosition, 1.0);
        vPositionEye3 = vertexPositionEye4.xyz / vertexPositionEye4.w;

        // Transform the normal to eye coordinates and send to fragment shader
        vNormalEye = normalize(uNMatrix * aVertexNormal);

        // Transform the geometry
        gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
        vTextureCoordinates = aTextureCoordinates;
      }
    </script>

    <!-- Fragment shader script -->
    <script id="shader-fs" type="x-shader/x-fragment">
      precision mediump float;
      varying vec2 vTextureCoordinates;
      varying vec3 vNormalEye;
      varying vec3 vPositionEye3;

      uniform vec3 uAmbientLightColor;
      uniform vec3 uDiffuseLightColor;
      uniform vec3 uSpecularLightColor;
      uniform vec3 uLightPosition;
      uniform vec3 uSpotDirection;
      uniform sampler2D uSampler;

      const float shininess = 32.0;
      const float spotExponent = 40.0;

      // cutoff angle for spot light
      const float spotCosCutoff = 0.97; // Corresponds to 14 degrees

      vec3 lightWeighting = vec3(0.0, 0.0, 0.0);

      void main() {
        // Calculate the vector (L) to the light source
        vec3 vectorToLightSource = normalize(uLightPosition - vPositionEye3);

        // Calculate N dot L for diffuse lighting
        float diffuseLightWeighting = max(dot(vNormalEye, vectorToLightSource), 0.0);

        // We only do spot and specular light calculations if we
        // have diffuse light term.
        if (diffuseLightWeighting > 0.0) {
          // Calculate the intensity of spot light in the direction of
          // vectorToLightSource.
          float spotEffect = dot(normalize(uSpotDirection), normalize(-vectorToLightSource));

          // Check that we are inside the spot light cone
          if (spotEffect > spotCosCutoff) {
            spotEffect = pow(spotEffect, spotExponent);

            // Calculate the reflection vector (R) needed for specular light
            vec3 reflectionVector = normalize(reflect(-vectorToLightSource, vNormalEye));

            // Calculate view vector (V) in eye coordinates as
            // (0.0, 0.0, 0.0) - vPositionEye3
            vec3 viewVectorEye = -normalize(vPositionEye3);
            float rdotv = max(dot(reflectionVector, viewVectorEye), 0.0);
            float specularLightWeighting = pow(rdotv, shininess);
            lightWeighting =
              spotEffect * uDiffuseLightColor * diffuseLightWeighting +
              spotEffect * uSpecularLightColor * specularLightWeighting;
          }
        }

        // Always ass the ambient light
        lightWeighting += uAmbientLightColor;

        // Sample the texture
        vec4 texelColor = texture2D(uSampler, vTextureCoordinates);

        // Modulate texel color with lightweighthing and write as final color
        gl_FragColor = vec4(lightWeighting.rgb * texelColor.rgb, texelColor.a);
      }
    </script>

    <!-- Script for WebGL program -->
    <script type="text/javascript" src="./js/week11.js"></script>
</head>
<body onload="startup();">
    <canvas id="myGlCanvas" width="500" height="500"></canvas>
    <div id="fps-counter"> FPS: <span id="fps">--</span></div>
</body>
</html>